#!/usr/bin/perl

package SML::Division;

use Moose;

extends 'SML::Part';

use version; our $VERSION = qv('2.0.0');

use namespace::autoclean;

use Digest::SHA qw(sha1);

use Log::Log4perl qw(:easy);
with 'MooseX::Log::Log4perl';
my $logger = Log::Log4perl::get_logger('sml.Division');

use lib "..";

######################################################################
######################################################################
##
## Public Attributes
##
######################################################################
######################################################################

has '+id' =>
  (
   required => 1,
  );

######################################################################

has number =>
  (
   is       => 'ro',
   isa      => 'Str',
   reader   => 'get_number',
   writer   => 'set_number',
   default  => '',
  );

# This "number" is often used to uniquely identify the division in a
# document presentation.  The number is generated by the parser.
#
# Sections have numbers like "2.13.4".
#
# Other structures like tables and figures have numbers like "3-12".
# The first number (3) is the toplevel section in which the structure
# appears.  The second number (12) is simply a one-up count
# representing that structure (i.e. table) within the toplevel
# section.
#
# Entities also have numbers like "7-95".

######################################################################

has previous_number =>
  (
   is       => 'ro',
   isa      => 'Str',
   reader   => 'get_previous_number',
   writer   => 'set_previous_number',
   default  => '',
  );

# This is the number of the "previous" division (in the case this
# division represents a structure or entity that exists in a
# sequence).
#
# For instance, if this could be the previous number of a 'SLIDE' in a
# sequence of 'SLIDE's.

######################################################################

has next_number =>
  (
   is       => 'ro',
   isa      => 'Str',
   reader   => 'get_next_number',
   writer   => 'set_next_number',
   default  => '',
  );

# This is the number of the "next" division (in the case this division
# represents a structure or entity that exists in a sequence).
#
# For instance, if this could be the number of the next 'SLIDE' in a
# sequence of 'SLIDE's.

######################################################################

has containing_division =>
  (
   is        => 'ro',
   isa       => 'SML::Division',
   reader    => 'get_containing_division',
   writer    => 'set_containing_division',
   predicate => 'has_containing_division',
  );

# The division to which this one belongs; the division containing this
# one; the division of which this one is a part.

######################################################################

has origin_line =>
  (
   is        => 'ro',
   isa       => 'SML::Line',
   reader    => 'get_origin_line',
   predicate => 'has_origin_line',
  );

# If this division was included via the `include' mechanism, this is
# the SML::Line object that included the division.

######################################################################

has sha_digest =>
  (
   is       => 'ro',
   isa      => 'Str',
   reader   => 'get_sha_digest',
   lazy     => 1,
   builder  => '_build_sha_digest',
  );

######################################################################
######################################################################
##
## Public Methods
##
######################################################################
######################################################################

sub add_division {

  my $self     = shift;
  my $division = shift;

  unless ( ref $division and $division->isa('SML::Division') )
    {
      $logger->error("NOT A DIVISION \'$division\'");
      return 0;
    }

  my $id = $division->get_id;

  if ( exists $self->_get_division_hash->{$id} )
    {
      $logger->warn("DIVISION ALREADY EXISTS $id");
    }

  $self->_get_division_hash->{$id} = $division;

  return 1;
}

######################################################################

sub add_part {

  # A part of a division must be either a block or a division.

  my $self = shift;
  my $part = shift;

  # validate input
  unless ( ref $part and ( $part->isa('SML::Block') or $part->isa('SML::Division') ) )
    {
      $logger->error("CAN'T ADD PART TO DIVISION \'$part\' is not a block or division");
      return 0;
    }

  $part->set_containing_division($self);

  push @{ $self->get_part_list }, $part;

  $logger->trace("add part $part");

  return 1;
}

######################################################################

sub add_attribute {

  # What's the difference between attributes and properties?
  #
  # Attributes are not declared in the ontology and therefore cannot
  # be validated like properties.

  my $self    = shift;
  my $element = shift;

  unless ( ref $element and $element->isa('SML::Element') )
    {
      $logger->error("NOT AN ELEMENT \'$element\'");
      return 0;
    }

  my $library    = $self->get_library;
  my $ah         = $self->_get_attribute_hash;
  my $term       = $element->get_term;
  my $definition = $element->get_definition;

  if ( not exists $ah->{$term} )
    {
      $ah->{$term} = [];
    }

  push @{ $ah->{$term} }, $definition;

  return 1;
}

######################################################################

sub contains_division_with_id {

  # Return 1 if the division contains a division with the specified ID.

  my $self = shift;
  my $id   = shift;

  unless ( $id )
    {
      $logger->error("YOU MUST SPECIFY AN ID");
      return 0;
    }

  foreach my $division (@{ $self->get_division_list })
    {
      if ( $division->get_id eq $id )
	{
	  return 1;
	}

      elsif ( $division->contains_division_with_id($id) )
	{
	  return 1;
	}
    }

  return 0;
}

######################################################################

sub has_attribute {

  my $self      = shift;
  my $attribute = shift;

  unless ( $attribute )
    {
      $logger->error("YOU MUST SPECIFY ATTRIBUTE");
      return 0;
    }

  if ( exists $self->_get_attribute_hash->{$attribute} )
    {
      return 1;
    }
  else
    {
      return 0;
    }
}

######################################################################

sub contains_division_with_name {

  # Return 1 if this division contains one or more divisions with the
  # specified name.  For instance, if you wanted to know if this
  # division contains any tables:
  #
  #   if ( $division->contains_division_with_name('TABLE') )
  #     {
  #       # do something...
  #     }

  my $self = shift;
  my $name = shift;

  unless ( $name )
    {
      $logger->error("CAN'T DETERMINE IF DIVISION CONTAINS ANOTHER, MISSING ARGUMENTS");
      return 0;
    }

  my $library  = $self->get_library;
  my $ontology = $library->get_ontology;

  unless ( $ontology->allows_division_name($name) )
    {
      $logger->error("CAN'T DETERMINE IF DIVISION CONTAINS ANOTHER, \'$name\' NOT ALLOWED");
      return 0;
    }

  foreach my $part (@{ $self->get_division_list })
    {
      my $part_name = $part->get_name;

      if ( $part_name eq $name )
	{
	  return 1;
	}
    }

  return 0;
}

######################################################################

sub contains_element_with_name {

  # Return 1 if this division contains one or more elements with the
  # specified name.  For instance, if you wanted to know if this
  # division contains any images:
  #
  #   if ( $division->contains_element_with_name('image') )
  #     {
  #       # do something...
  #     }

  my $self = shift;
  my $name = shift;

  unless ( $name )
    {
      $logger->error("CAN'T DETERMINE IF DIVISION CONTAINS ELEMENT, MISSING ARGUMENTS");
      return 0;
    }

  foreach my $element (@{ $self->get_element_list })
    {
      my $element_name = $element->get_name;

      if ( $element_name eq $name )
	{
	  return 1;
	}
    }

  return 0;
}

######################################################################

sub get_list_of_divisions_with_name {

  # Return a list of division objects (within this one) with the
  # specified name.  For instance, return a list of sections within
  # this division.

  my $self = shift;
  my $name = shift;

  unless ( $name )
    {
      $logger->error("CAN'T GET LIST OF DIVISIONS WITH NAME, MISSING ARGUMENTS");
      return 0;
    }

  my $list = [];

  foreach my $part (@{ $self->get_division_list })
    {
      my $part_name = $part->get_name;

      if ( $part_name eq $name )
	{
	  push @{ $list }, $part;
	}
    }

  return $list;
}

######################################################################

sub get_list_of_elements_with_name {

  # Return a list of element objects (within this division) with the
  # specified name.  For instance, return a list of image elements
  # within this division.

  my $self = shift;
  my $name = shift;

  unless ( $name )
    {
      $logger->error("CAN'T GET LIST OF ELEMENTS WITH NAME, MISSING ARGUMENTS");
      return 0;
    }

  my $list = [];

  foreach my $element (@{ $self->get_element_list })
    {
      my $element_name = $element->get_name;

      if ( $element_name eq $name )
	{
	  push @{ $list }, $element;
	}
    }

  return $list;
}

######################################################################

sub get_division_list {

  # Return an ordered list of divisions within this one.  Recurse to
  # provide a complete ordered sequence of divisions, depth first.

  my $self = shift;

  my $list = [];                        # division list

  no warnings 'recursion';

  foreach my $part (@{ $self->get_part_list })
    {
      if ( $part->isa('SML::Division') )
	{
	  push @{ $list }, $part;
	  push @{ $list }, @{ $part->get_division_list };
	}
    }

  return $list;
}

######################################################################

sub get_block_list {

  # Return an ordered list of blocks within this division.

  my $self = shift;

  my $list = [];                        # block list

  no warnings 'recursion';

  foreach my $part (@{ $self->get_part_list })
    {
      if ( $part->isa('SML::Block' ) )
	{
	  push @{ $list }, $part;
	}

      elsif ( $part->isa('SML::Division') )
	{
	  push @{ $list }, @{ $part->get_block_list };
	}

      else
	{
	  $logger->error("THIS SHOULD NEVER HAPPEN");
	}
    }

  return $list;
}

######################################################################

sub get_string_list {

  # Return an ordered list of strings withing this division.

  my $self = shift;

  my $list = [];                        # string list

  no warnings 'recursion';

  foreach my $block (@{ $self->get_block_list })
    {
      $self->_add_parts_to_list($block,$list);
    }

  return $list;
}

######################################################################

sub get_element_list {

  # Return an ordered list of elements within this division.

  my $self = shift;

  my $list = [];                        # element list

  foreach my $block (@{ $self->get_block_list })
    {
      if ( $block->isa('SML::Element') )
	{
	  push @{ $list }, $block;
	}

    }

  return $list;
}

######################################################################

sub get_line_list {

  my $self = shift;

  my $list = [];                        # line list

  no warnings 'recursion';

  foreach my $part (@{ $self->get_part_list })
    {
      push @{ $list }, @{ $part->get_line_list };
    }

  return $list;
}

######################################################################

sub get_first_part {

  my $self = shift;

  my $part_list = $self->get_part_list;

  unless ( $self->contains_parts )
    {
      $logger->error("CAN'T GET FIRST PART, DIVISION HAS NO PARTS");
      return 0;
    }

  return $self->get_part_list->[0];
}

######################################################################

sub get_first_line {

  my $self = shift;

  unless ( $self->contains_parts )
    {
      $logger->error("CAN'T GET FISRT LINE, DIVISION HAS NO PARTS");
      return 0;
    }

  my $first_part = $self->get_first_part;

  return $first_part->get_first_line;
}

######################################################################

sub get_containing_document {

  # Return the document to which this division belongs (or undef).

  my $self = shift;

  my $division = $self;

  while ( ref $division )
    {
      my $name = $division->get_name;

      if ( $name eq 'DOCUMENT' )
	{
	  return $division;
	}

      else
	{
	  $division = $division->get_containing_division;
	}
    }

  # $logger->error("CAN'T GET DOCUMENT");
  return 0;
}

######################################################################

sub get_location {

  # Return the location (filepec + line number) of the beginning of
  # this division.

  my $self = shift;

  my $block = $self->get_block_list->[0];

  if ( $block )
    {
      return $block->get_location;
    }

  else
    {
      return 'UNKNOWN';
    }
}

######################################################################

sub get_containing_section {

  # Return the section containing this division.  If not in a section,
  # return undef.

  my $self = shift;

  my $division = $self;

  while ( $division )
    {
      if ( $division->isa('SML::Section') )
	{
	  return $division;
	}

      else
	{
	  $division = $division->get_containing_division;
	}
    }

  # $logger->error("CAN'T GET SECTION");
  return 0;
}

######################################################################

sub is_in_a {

  # Return 1 if this division is in a division of "name" (even if it
  # is buried several divisions deep).

  my $self = shift;
  my $name = shift;

  unless ( $name )
    {
      $logger->error("CAN'T CHECK IF DIVISION IS IN ANOTHER, MISSING ARGUMENT");
      return 0;
    }

  my $division = $self;

  while ( ref $division and $division->isa('SML::Division') )
    {
      my $division_name = $division->get_name;

      if ( $division_name eq $name )
	{
	  return 1;
	}

      else
	{
	  $division = $division->get_containing_division;
	}
    }

  return 0;
}

######################################################################

sub get_content {

  my $self = shift;

  my $content = q{};

  foreach my $block (@{ $self->get_block_list })
    {
      $content .= $block->get_content . "\n\n";
    }

  return $content;
}

######################################################################
######################################################################
##
## Private Attributes
##
######################################################################
######################################################################

has division_hash =>
  (
   is        => 'ro',
   isa       => 'HashRef',
   reader    => '_get_division_hash',
   default   => sub {{}},
  );

# This datastructure contains division objects (contained by this one)
# indexed by division ID.
#
#   $dh->{$divid} = $division;

######################################################################

has attribute_hash =>
  (
   is        => 'ro',
   isa       => 'HashRef',
   reader    => '_get_attribute_hash',
   default   => sub {{}},
  );

######################################################################
######################################################################
##
## Private Methods
##
######################################################################
######################################################################

sub BUILD {

  my $self = shift;

  my $id      = $self->get_id;
  my $library = $self->get_library;

  if ( $id )
    {
      my $id_element = SML::Element->new
	(
	 name    => 'id',
	 library => $self->get_library,
	 value   => $id,
	);

      $library->add_property_value($id,'id',$id,$id_element);
    }

  return 1;
}

######################################################################

sub _build_sha_digest {

  my $self = shift;

  my $sha  = Digest::SHA->new;
  my $data = $self->get_content;

  $sha->add($data);

  return $sha->hexdigest;
}

######################################################################

sub _add_parts_to_list {

  my $self = shift;
  my $part = shift;
  my $list = shift;

  unless ( $part and $list )
    {
      $logger->error("CAN'T ADD PARTS TO LIST, MISSING ARGUMENTS");
      return 0;
    }

  unless ( $part->isa('SML::Part') )
    {
      $logger->error("CAN'T ADD PARTS TO LIST, PART IS NOT A PART $part");
      return 0;
    }

  foreach my $subpart (@{ $part->get_part_list })
    {
      push @{$list}, $subpart;

      if ( $subpart->contains_parts )
	{
	  $self->_add_parts_to_list($subpart,$list);
	}
    }

  return 1;
}

######################################################################

no Moose;
__PACKAGE__->meta->make_immutable;
1;

__END__

=head1 NAME

C<SML::Division> - a L<"SML::Part"> which is a contiguous sequence of
L<"SML::Block">s.

=head1 VERSION

2.0.0

=head1 SYNOPSIS

  extends SML::Part

  my $division = SML::Division->new
                   (
                     id      => $id,
                     library => $library,
                     name    => $name,
                   );

  my $string   = $division->get_number;
  my $boolean  = $division->set_number;
  my $string   = $division->get_previous_number;
  my $boolean  = $division->set_previous_number($number);
  my $string   = $division->get_next_number;
  my $boolean  = $division->set_next_number($number);
  my $division = $division->get_containing_division;
  my $boolean  = $division->set_containing_division($division);
  my $boolean  = $division->has_containing_division;
  my $boolean  = $division->has_valid_syntax;
  my $boolean  = $division->has_valid_semantics;
  my $boolean  = $division->has_valid_property_cardinality;
  my $boolean  = $division->has_valid_property_values;
  my $boolean  = $division->has_valid_infer_only_conformance;
  my $boolean  = $division->has_valid_required_properties;
  my $boolean  = $division->has_valid_composition;
  my $boolean  = $division->has_valid_id_uniqueness;

  my $boolean  = $division->add_division($division);
  my $boolean  = $division->add_part($part);
  my $boolean  = $division->add_property($property);
  my $boolean  = $division->add_attribute($element);
  my $boolean  = $division->contains_division_with_name($name);
  my $boolean  = $division->contains_division_with_id($id);
  my $boolean  = $division->has_property($property_name);
  my $boolean  = $division->has_property_value($property_name,$value);
  my $boolean  = $division->has_attribute($attribute_name);
  my $list     = $division->get_division_list;
  my $list     = $division->get_list_of_divisions_with_name('SECTION');
  my $list     = $division->get_block_list;
  my $list     = $division->get_element_list;
  my $list     = $division->get_line_list;
  my $part     = $division->get_first_part;
  my $line     = $division->get_first_line;
  my $document = $division->get_containing_document;
  my $string   = $division->get_location;
  my $section  = $division->get_containing_section;
  my $boolean  = $division->is_in_a($division_name);
  my $boolean  = $division->validate;

=head1 DESCRIPTION

A L<"SML::Division"> is a contiguous sequence of L<"SML::Block">s.  A
division has an unambiguous beginning and end.  Sometimes the
beginning and end are explicit and other times they are implicit.
Divisions may contain other divisions.

=head1 METHODS

=head2 get_number

=head2 set_number

=head2 get_previous_number

=head2 set_previous_number($number)

=head2 get_next_number

=head2 set_next_number($number)

=head2 get_containing_division

=head2 set_containing_division($division)

=head2 has_containing_division

=head2 has_valid_syntax

=head2 has_valid_semantics

=head2 has_valid_property_cardinality

=head2 has_valid_property_values

=head2 has_valid_infer_only_conformance

=head2 has_valid_required_properties

=head2 has_valid_composition

=head2 has_valid_id_uniqueness

=head2 add_division($division)

=head2 add_part($part)

=head2 add_property($property)

=head2 add_property_element($element)

=head2 add_attribute($element)

=head2 contains_division($id)

=head2 has_attribute($attribute_name)

=head2 get_division_list

=head2 has_sections

=head2 get_list_of_divisions_with_name($name)

=head2 get_block_list

=head2 get_element_list

=head2 get_line_list

=head2 get_data_segment_line_list

=head2 get_narrative_line_list

=head2 get_first_part

=head2 get_first_line

=head2 get_containing_document

=head2 get_location

=head2 get_containing_section

=head2 is_in_a($division_type)

=head2 validate

=head1 AUTHOR

Don Johnson (drj826@acm.org)

=head1 LICENSE AND COPYRIGHT

Copyright (c) 2012,2013 Don Johnson (drj826@acm.org)

Distributed under the terms of the Gnu General Public License (version
2, 1991)

This software is distributed in the hope that it will be useful, but
WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
License for more details.

MODIFICATIONS AND ENHANCEMENTS TO THIS SOFTWARE OR WORKS DERIVED FROM
THIS SOFTWARE MUST BE MADE FREELY AVAILABLE UNDER THESE SAME TERMS.

=cut
